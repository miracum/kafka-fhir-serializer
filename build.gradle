plugins {
    id "java-library"
    id "maven-publish"
    id "signing"
    id "com.diffplug.spotless" version "5.6.1"
}

group = "org.miracum"
version = "1.0.1"

java {
    withJavadocJar()
    withSourcesJar()
}

repositories {
    // Use jcenter for resolving dependencies.
    // You can declare any Maven/Ivy/file repository here.
    jcenter()
}

dependencies {
    // This dependency is used internally, and not exposed to consumers on their own compile classpath.
    implementation "ca.uhn.hapi.fhir:hapi-fhir-base:5.1.0"
    implementation "org.apache.kafka:kafka-clients:2.6.0"

    // Use JUnit Jupiter API for testing.
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.6.0"
    testImplementation "ca.uhn.hapi.fhir:hapi-fhir-structures-r4:5.0.0"

    // Use JUnit Jupiter Engine for testing.
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.6.0"
}

test {
    // Use junit platform for unit tests
    useJUnitPlatform()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = "kafka-fhir-serializer"
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = "Kafka FHIR® Serializer"
                description = "Support for serializing and deserializing FHIR® resources in Apache Kafka."
                url = "https://github.com/miracum/kafka-fhir-serializer"
                licenses {
                    license {
                        name = "GNU General Public License v3.0"
                        url = "https://www.gnu.org/licenses/gpl-3.0.txt"
                    }
                }
                developers {
                    developer {
                        id = 'chgl'
                    }
                }
                scm {
                    connection = "scm:git:git://github.com/miracum/kafka-fhir-serializer.git"
                    developerConnection = "scm:git:ssh://github.com:miracum/kafka-fhir-serializer.git"
                    url = "https://github.com/miracum/kafka-fhir-serializer/tree/master"
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith("SNAPSHOT") ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = ossrhUsername
                password = ossrhPassword
            }
        }
    }
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption("html5", true)
    }
}

signing {
    sign publishing.publications.mavenJava
}

spotless {
    java {
        importOrder()
        removeUnusedImports()
        googleJavaFormat()
    }
}
